import org.panteleyev.jpackage.ImageType
import org.panteleyev.jpackage.JPackageTask
import org.springframework.boot.gradle.tasks.bundling.BootJar

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.2'
    id 'io.spring.dependency-management' version '1.1.0'
    //id 'org.graalvm.buildtools.native' version '0.9.20'
    id 'com.vaadin' version '24.1.6'

    //Jpackage for building native installers
    id("org.panteleyev.jpackageplugin") version "1.5.2"
}

group = 'online.hatsunemiku'
version = '0.9.0'
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        name = "Vaadin Addons"
        url = "https://maven.vaadin.com/vaadin-addons"
    }
}

ext {
    set('vaadinVersion', "24.1.6")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation('com.vaadin:vaadin-spring-boot-starter') {
        if (vaadin.productionMode) {
            exclude group: "com.vaadin", module: "vaadin-dev-server"
        }
    }
    implementation 'org.jetbrains:annotations:24.0.0'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.1.5'

    //Feign Client
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.0.4'
    implementation 'io.github.openfeign:feign-jackson:12.2'
    implementation 'io.github.openfeign.form:feign-form:3.8.0'
    implementation 'io.github.openfeign.form:feign-form-spring:3.8.0'
    implementation 'io.github.openfeign:feign-okhttp:12.4'


    implementation 'pl.unforgiven:superfields:0.18.3'
    implementation 'com.squareup.okhttp3:okhttp:4.11.0'

    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    //vaadin directory plugins
    implementation 'in.virit:viritin:2.0.1'
    implementation 'org.vaadin.addons.online.hatsunemiku.diamond:FlowSwiper:1.2.0'
}

dependencyManagement {
    imports {
        mavenBom "com.vaadin:vaadin-bom:${vaadinVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}


//new task to build bootJar
tasks.register('buildReleaseJar', BootJar) {
    group = 'build'

    dependsOn('vaadinBuildFrontend')
    enabled = true
    vaadin.productionMode = true
    archiveFileName = "Tachidesk-VaadinUI-${version}.jar"
    mainClass = 'online.hatsunemiku.tachideskvaadinui.TachideskVaadinUiApplication'
    setTargetJavaVersion(targetCompatibility)

    //fat jar
    classpath(sourceSets.main.runtimeClasspath)

    launchScript()
}

tasks.register('buildWindowsMsi', WindowsInstallTask) {
    type = ImageType.MSI
}

tasks.register('buildWindowsExe', WindowsInstallTask) {
    type = ImageType.EXE
}

class InstallTask extends JPackageTask {
    InstallTask() {
        super()
        dependsOn('buildReleaseJar')
        group = 'installers'
        enabled = true

        aboutUrl = "https://github.com/aless2003/Tachidesk-VaadinUI"
        appDescription = "A simple Web UI for Tachidesk written in Java with Vaadin"
        appName = "Tachidesk Vaadin UI"
        appVersion = "${project.version}"
        icon = "package/icons/icon.ico"
        licenseFile = "LICENSE"
        mainClass = "org.springframework.boot.loader.JarLauncher"
        mainJar = "Tachidesk-VaadinUI-${project.version}.jar"
        vendor = "Aless2003"
        verbose = false
        input = "build/libs"
        destination = "build/installer"
    }
}

class WindowsInstallTask extends InstallTask {

    //inject annotation with full qualified class name
    WindowsInstallTask() {
        super()

        //winConsole = true //enable only for debugging
        winDirChooser = true
        winHelpUrl = "https://github.com/aless2003/Tachidesk-VaadinUI"
        winMenu = true
        winMenuGroup = "DiamondMiku"
        winPerUserInstall = true
        winShortcut = true
        winShortcutPrompt = true
        winUpdateUrl = "https://github.com/aless2003/Tachidesk-VaadinUI"
        winUpgradeUuid = "1fd89884-c606-4304-b9f0-f82c47ff7676"
    }
}